plugins {
    id 'java'
    id "org.openapi.generator" version "5.1.0"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'
    // https://mvnrepository.com/artifact/io.swagger/swagger-annotations
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

def modelJavDir = "$buildDir/generated".toString()

sourceSets{
    main{
        java{
            srcDir modelJavDir
        }
    }
}

openApiGenerate {
    globalProperties = [
            apis: "false",
            modelDocs: "false",
            models: "User,Pet"
    ]
    generatorName = "java"
    generateModelTests = false
    inputSpec = "$rootDir/src/main/resources/schema/my_schema.json".toString()
    outputDir = modelJavDir
    modelPackage = "org.openapi.example.model"
    configOptions = [
            dateLibrary: "java8",
            serializationLibrary: "jackson",
            library: "jersey1"
    ]
}

compileJava.dependsOn tasks.openApiGenerate

